#+startup: overview

#+begin_center
*Gunnar's Doom Emacs*

_Config as a presentation-ready narrative_
#+end_center

#+begin_quote
"Sharpen the tools, then craft the work." — shop mantra
#+end_quote

* Header
#+begin_src emacs-lisp
;;; $DOOMDIR/config.el -*- lexical-binding: t; -*-
#+end_src


* General Settings
** Functions
** Settings
*** Orderless (CamelCase completion)
Enable CamelCase matching for completion.
#+begin_src emacs-lisp
(after! orderless
  (setq orderless-matching-styles '(orderless-literal
                                   orderless-regexp
                                   orderless-initialism
                                   orderless-flex)))
#+end_src

*** Auto-revert
Enable automatic reversion of buffers when files change on disk.
#+begin_src emacs-lisp
(global-auto-revert-mode 1)
#+end_src

*** General Configuration
#+begin_src emacs-lisp
(cond ((eq system-type 'gnu/linux) (setenv "SSH_AUTH_SOCK" "/run/user/1000/gnupg/S.gpg-agent.ssh"))
      ((eq system-type 'darwin)    (setenv "SSH_AUTH_SOCK" "/Users/gunnar.bastkowski/.gnupg/S.gpg-agent.ssh"))
      (t                           (setenv "SSH_AUTH_SOCK" "")))

(setq! auth-sources                       '(password-store))

(setq! fill-column                        120
       fci-rule-color                     "#555555"
       fci-rule-width                     1)
(global-display-fill-column-indicator-mode)

(setq! tab-width                          8)

(setq! user-full-name                     "Gunnar Bastkowski"
       user-mail-address                  "gunnar@bastkowski.name")

(setq! which-key-use-C-h-commands         t)

(setq! package-install-upgrade-built-in   t)


;; Override function because we are generating config.el from this file
(defun doom/goto-private-config-file ()
  "Open your private config.el file."
  (interactive)
  (find-file (expand-file-name "config.org" doom-user-dir)))

(setq! aw-scope 'global)
#+end_src

*** Spell checking dictionary
#+begin_src emacs-lisp
(setq! ispell-personal-dictionary
       (expand-file-name "ispell.pws" doom-user-dir))
#+end_src

*** Claude Code
Make Claude's IDE terminal integration use the `eat' backend inside Emacs.
#+begin_src emacs-lisp
(setq! claude-code-ide-terminal-backend 'eat)
#+end_src

*** MCP Emacs helpers
Load the `mcp-emacs' package (installed via `packages.el`) and make sure the
Emacs server is running before the MCP Node server connects.
#+begin_src emacs-lisp
(use-package! mcp-emacs :defer nil)
#+end_src

** Git/Magit Settings
#+begin_src emacs-lisp
(setq! git-commit-summary-max-length 70)
#+end_src


* Appearance
** Theme
There are two ways to load a theme. Both assume the theme is installed and available.
You can either set `doom-theme' or manually load a theme with the `load-theme' function.
This is the default:
#+begin_src emacs-lisp
(setq! doom-theme 'gunnar)
#+end_src

** Folds
*** Functions
Folds by indentation
Found at https://stackoverflow.com/questions/1587972/how-to-display-indentation-guides-in-emacs/4459159#4459159
and decided that I like it
#+begin_src emacs-lisp
(defun aj-toggle-fold ()
  "Toggle fold all lines larger than indentation on current line"
  (interactive)
  (let ((col 1))
    (save-excursion
      (back-to-indentation)
      (setq col (+ 1 (current-column)))
      (set-selective-display
       (if selective-display nil (or col 1))))))

;; (map! :leader :desc "Toggle fold by indentation" :n "aj" #'aj-toggle-fold)
#+end_src
** Font
Doom exposes five (optional) variables for controlling fonts in Doom:
- `doom-font' -- the primary font to use
- `doom-variable-pitch-font' -- a non-monospace font (where applicable)
- `doom-big-font' -- used for `doom-big-font-mode'; use this for presentations or streaming.
- `doom-symbol-font' -- for symbols
- `doom-serif-font' -- for the `fixed-pitch-serif' face

#+begin_src emacs-lisp
(cond ((eq system-type 'gnu/linux)
       (setq! doom-font (font-spec :family "IosevkaNerdFont" :size 18)))
      (t
       (setq! doom-font (font-spec :family "Iosevka Nerd Font" :size 15))))

(global-prettify-symbols-mode t)
;; (map! :leader :desc "Toggle global prettify symbols mode" :n "aP" #'global-prettify-symbols-mode)
#+end_src

If you or Emacs can't find your font, use 'M-x describe-font' to look them up,
~M-x eval-region~ to execute elisp code, and 'M-x doom/reload-font'
to refresh your font settings.

If Emacs still can't find your font, it likely wasn't installed correctly.
Font issues are rarely Doom issues!

** Line Numbers
This determines the style of line numbers in effect. If set to `nil', line
numbers are disabled. For relative line numbers, set this to `relative'.

#+begin_src emacs-lisp
(setq display-line-numbers-type     t
      display-line-numbers-width    4)
#+end_src


* Editing
** Duplicate
*** Functions
from https://www.emacswiki.org/emacs/CopyingWholeLines
#+BEGIN_SRC emacs-lisp
(defun duplicate-line-or-region (&optional n)
  "Duplicate current line, or region if active.
    With argument N, make N copies.
    With negative N, comment out original line and use the absolute value."
  (interactive "*p")
  (let ((use-region (use-region-p)))
    (save-excursion
      (let ((text (if use-region
                      (buffer-substring (region-beginning) (region-end))
                    (prog1 (thing-at-point 'line)
                      (end-of-line)
                      (if (< 0 (forward-line 1)) ;Go to beginning of next line, or make a new one
                          (newline))))))
        (dotimes (i (abs (or n 1)))     ;Insert N times, or once if not specified
          (insert text))))
    (if use-region nil                  ;Only if we're working with a line (not a region)
      (let ((pos (- (point) (line-beginning-position)))) ;Save column
        (if (> 0 n)                             ;Comment out original with negative arg
            (comment-region (line-beginning-position) (line-end-position)))
        (forward-line 1)
        (forward-char pos)))))
#+END_SRC

*** Keybindings
#+BEGIN_SRC emacs-lisp
(map! :leader :desc "Duplicate line or region" :n "id" #'duplicate-line-or-region)
#+END_SRC


* Languages
** Avro
#+begin_src emacs-lisp
(use-package! avdl-mode)
#+end_src

** PlantUML
#+begin_src emacs-lisp
(use-package! plantuml-mode)
#+end_src

** Scala
*** Settings
**** General
#+begin_src emacs-lisp
(setq! scala-auto-insert-asterisk-in-comments t
       scala-sbt-window-position              nil
       scala-enable-gtags                     t)
#+end_src
**** Scala-Indent
#+begin_src emacs-lisp
(setq! scala-indent:use-javadoc-style nil)
#+end_src
**** LSP
***** UniteAI
#+begin_src emacs-lisp
;; UniteAI LSP Commands
(defun lsp-stop ()
  (interactive)
  (let* ((doc (lsp--text-document-identifier)))
    (lsp-request "workspace/executeCommand"
                 `(:command "command.stop"
                    :arguments ,(vector doc)))))

(defun lsp-openai-gpt ()
  (interactive)
  (unless (region-active-p)
    (error "No region selected"))
  (let* ((doc (lsp--text-document-identifier))
         (range (list :start (lsp--point-to-position (region-beginning))
                      :end (lsp--point-to-position (region-end)))))
    (lsp-request "workspace/executeCommand"
                 `(:command "command.openaiAutocompleteStream"
                   :arguments ,(vector doc range "FROM_CONFIG_COMPLETION" "FROM_CONFIG")))))

(defun lsp-openai-chatgpt ()
  (interactive)
  (unless (region-active-p)
    (error "No region selected"))
  (let* ((doc (lsp--text-document-identifier))
         (range (list :start (lsp--point-to-position (region-beginning))
                      :end (lsp--point-to-position (region-end)))))
    (lsp-request "workspace/executeCommand"
                 `(:command "command.openaiAutocompleteStream"
                   :arguments ,(vector doc range "FROM_CONFIG_CHAT" "FROM_CONFIG")))))

(defun lsp-document ()
  (interactive)
  (unless (region-active-p)
    (error "No region selected"))
  (let* ((doc (lsp--text-document-identifier))
         (range (list :start (lsp--point-to-position (region-beginning))
                      :end (lsp--point-to-position (region-end)))))
    (lsp-request "workspace/executeCommand"
                 `(:command "command.document"
                   :arguments ,(vector doc range)))))

(define-minor-mode uniteai-mode
  "Minor mode for interacting with UniteAI server."
  :lighter " UniteAI"
  :keymap (let ((map (make-sparse-keymap)))
            (define-key map (kbd "C-c l s") 'lsp-stop)
            (define-key map (kbd "C-c l g") 'lsp-openai-gpt)
            (define-key map (kbd "C-c l c") 'lsp-openai-chatgpt)
            (define-key map (kbd "C-c l d") 'lsp-document)
            map))

(after! lsp-mode
  (lsp-register-client
   (make-lsp-client
    :new-connection (lsp-stdio-connection `("uniteai_lsp" "--stdio"))
    :priority -3
    :major-modes '(python-mode markdown-mode org-mode)
    :server-id 'uniteai
    :add-on? t))

  (setq lsp-tcp-connection-timeout 5.0))
#+end_src



* Mu4e
** TODO missing settings
(smtpmail-starttls-credentials  . '(("smtp.gmail.com" 587 nil nil)))
;; (smtpmail-auth-credentials      . '(("smtp.gmail.com" 587 "gunnar.bastkowski@gmail.com" nil)))
(smtpmail-default-smtp-server   . "smtp.gmail.com")
(smtpmail-smtp-server           . "smtp.gmail.com")
(smtpmail-smtp-service          . 587)))))

** Settings
#+begin_src emacs-lisp
(when (string-prefix-p "akiko" (system-name) t)
  (setq mu4e-maildir         (expand-file-name ".mail" (getenv "HOME"))
        mu4e-update-interval 60
        +mu4e-gmail-accounts '(("gunnar.bastkowski@gmail.com" . "/gunnar.bastkowski@gmail.com")))

  (after! mu4e
    (setq sendmail-program (executable-find "msmtp")
          send-mail-function #'smtpmail-send-it
          message-sendmail-f-is-evil t
          message-sendmail-extra-arguments '("--read-envelope-from")
          message-send-mail-function #'message-send-mail-with-sendmail))

  (set-email-account! "akiko.local"
    '((mu4e-sent-folder       . "/gunnar@akiko.local/Sent")
      (mu4e-drafts-folder     . "/gunnar@akiko.local/Drafts")
      (mu4e-trash-folder      . "/gunnar@akiko.local/Trash")
      (mu4e-refile-folder     . "/gunnar@akiko.local/Archive")
      (smtpmail-smtp-user     . "gunnar@akiko.local")
      (mu4e-compose-signature . "---\nKind Regards\nGunnar"))
    t)
  (set-email-account! "bastkowski.name"
    '((mu4e-sent-folder       . "/gunnar@bastkowski.name/Sent")
      (mu4e-drafts-folder     . "/gunnar@bastkowski.name/Drafts")
      (mu4e-trash-folder      . "/gunnar@bastkowski.name/Trash")
      (mu4e-refile-folder     . "/gunnar@bastkowski.name/Archive")
      (smtpmail-smtp-user     . "gunnar@bastkowski.name")
      (mu4e-compose-signature . "---\nKind Regards\nGunnar"))
    t)
  (set-email-account! "digitalstep.de"
    '((mu4e-sent-folder       . "/gunnar@digitalstep.de/Sent")
      (mu4e-drafts-folder     . "/gunnar@digitalstep.de/Drafts")
      (mu4e-trash-folder      . "/gunnar@digitalstep.de/Trash")
      (mu4e-refile-folder     . "/gunnar@digitalstep.de/Archive")
      (smtpmail-smtp-user     . "gunnar@digitalstep.de")
      (mu4e-compose-signature . "---\nKind Regards\nGunnar"))
    t)
  (set-email-account! "gmail.com"
    '((mu4e-sent-folder       . "/gunnar.bastkowski@gmail.com/Sent")
      (mu4e-drafts-folder     . "/gunnar.bastkowski@gmail.com/Drafts")
      (mu4e-trash-folder      . "/gunnar.bastkowski@gmail.com/Trash")
      (mu4e-refile-folder     . "/gunnar.bastkowski@gmail.com/Archive")
      (smtpmail-smtp-user     . "gunnar.bastkowski@gmail.com")
      (mu4e-compose-signature . "---\nKind Regards\nGunnar"))
    t)
  (set-email-account! "gmx.de"
    '((mu4e-sent-folder       . "/gbastkowski@gmx.de/Sent")
      (mu4e-drafts-folder     . "/gbastkowski@gmx.de/Drafts")
      (mu4e-trash-folder      . "/gbastkowski@gmx.de/Trash")
      (mu4e-refile-folder     . "/gbastkowski@gmx.de/Archive")
      (smtpmail-smtp-user     . "gbastkowski@gmx.de")
      (mu4e-compose-signature . "---\nKind Regards\nGunnar"))
    t)

  (setq mu4e-bookmarks      '((:name "Unread messages"       :query "flag:unread AND NOT flag:trashed"     :key 117)
                              (:name "Inbox"                 :query "maildir:/inbox/"                      :key 116)
                              (:name "Today's messages"      :query "date:today..now"                      :key 116)
                              (:name "Last 7 days"           :query "date:7d..now"                         :key 119  :hide-unread t)
                              ("flag:flagged" "Flagged messages" 102)
                              (:name "Messages with images"  :query "mime:image/*"                         :key 112))))
#+end_src


* AI
** *Copilot
*** Settings
accept completion from copilot and fallback to company
+begin_src emacs-lisp
(use-package! copilot
  :hook (prog-mode . copilot-mode)
  :bind (:map copilot-completion-map
              ("<tab>" . 'copilot-accept-completion)
              ("TAB" . 'copilot-accept-completion)
              ("C-TAB" . 'copilot-accept-completion-by-word)
              ("C-<tab>" . 'copilot-accept-completion-by-word)))
+end_src

** OpenAI
*** Settings
#+begin_src emacs-lisp
(setq! openai-playground-completion-api   "https://api.openai.com/v1/chat/completions"
       openai-playground-completion-model "gpt-4o-mini"
       openai-playground-key              (password-store-get "private/openai/emacs-api-key")
       openai-playground-project          (password-store-get "private/openai/default-project-id")
       openai-playground-org              (password-store-get "private/openai/default-org-id"))

#+end_src
*** Functions
#+begin_src emacs-lisp
(use-package! openai-playground)
#+end_src


* Org Mode
Most of the stuff here is to implement a GTD workflow.
Inspiration can be obtained from
- [[https://www.labri.fr/perso/nrougier/GTD/index.html][Get Things Done with Emacs]]
- [[https://blog.jethro.dev/posts/org_mode_workflow_preview/][Org-mode Workflow: A Preview]]
- [[http://doc.norang.ca/org-mode.html][Org Mode - Organize Your Life In Plain Text!]]
- [[https://vtimofeenko.com/posts/my-doom-emacs-setup/#orgmode-config][My doom-emacs setup | Vladimir Timofeenko's blog]]

** General
*** Settings
#+begin_src emacs-lisp
(setq! org-superstar-headline-bullets-list '("◉" "○" "⬥" "⬦" "▸" "▹")
       org-directory                        "~/org/"
       org-mobile-directory                 "~/org/"
       org-modules                         '(ol-bibtex org-habit))
#+end_src

**** Tags
#+begin_src emacs-lisp
(after! org
  (setq org-tag-alist                         '((:startgroup)
                                                ; Put mutually exclusive tags here
                                                (:endgroup)
                                                ("@errand" . ?E)
                                                ("@home" . ?H)
                                                ("@work" . ?W)
                                                ("note" . ?n)
                                                ("idea" . ?i)
                                                ("recurring" . ?r)
                                                ("thinking" . ?t)))
  (setq org-tags-column                       100))
#+end_src
**** Keywords
#+begin_src emacs-lisp
(after! org
  (setq org-todo-keywords         '((sequence "TODO(t)"  ; A task that needs doing & is ready to do
                                              "NEXT(n)"  ; next step in a project
                                              "STRT(s)"  ; A task that is in progress
                                              "WAIT(w)"  ; Something external is holding up this task
                                              "|"
                                              "DONE(d)"  ; successfully completed
                                              "KILL(k)") ; cancelled, aborted
                                    (sequence "[ ](T)"   ; needs doing
                                              "[-](S)"   ; in progress
                                              "[?](W)"   ; held up or paused
                                              "|"
                                              "[X](D)")  ; completed
                                    (sequence "|"
                                              "OKAY(o)"
                                              "YES(y)"
                                              "NO(n)"))
        org-todo-keyword-faces    '(("[-]"  . +org-todo-active)
                                    ("STRT" . +org-todo-active)
                                    ("NEXT" . +org-todo-active)
                                    ("[?]"  . +org-todo-onhold)
                                    ("WAIT" . +org-todo-onhold)
                                    ("NO"   . +org-todo-cancel)
                                    ("DONE" . +org-todo-cancel)
                                    ("KILL" . +org-todo-cancel))))
#+end_src

*** Functions
#+begin_src emacs-lisp
(defun org-insert-src-block (language)
  "Insert a `SRC-CODE-TYPE' type source code block in org-mode."
  (interactive (let ((languages
                      '("C" "C++" "R" "clojure" "css" "ditaa" "dot" "emacs-lisp" "gnuplot" "haskell" "http"
                        "java" "js" "latex" "lisp" "org" "plantuml" "python" "ruby"
                        "sass" "scala" "sh" "sql" "sqlite")))
                 (list (ido-completing-read "Language: " languages))))
  (newline-and-indent)
  (insert (format "#+begin_src %s\n\n#+end_src" language))
  (previous-line 1)
  (org-edit-src-code))
#+end_src

*** Keybindings
#+begin_src emacs-lisp
(map! :leader :desc "Insert source block"         :n "mws" #'org-insert-src-block)
#+end_src


** Agenda & Journal
*** Settings
**** Clock, Diary, and Journal
#+begin_src emacs-lisp
(setq! org-clock-persist 'history)
(org-clock-persistence-insinuate)

(setq! diary-file                             (concat org-directory "diary"))
(setq! timeclock-file                         (concat org-directory "timeclock"))
(setq! org-journal-carryover-items            "TODO=\"TODO|NEXT\""
       org-journal-dir                        "~/org/journal/"
       org-journal-file-format                "%Y/%m/%d.org"
       org-journal-date-format                "Journal"
       org-journal-file-header                (lambda (time) (concat
                                                              "#+TITLE: " (format-time-string "%Y-%m-%d") "\n"
                                                              "#+CATEGORY: Journal\n"
                                                              "\n\n"))
       org-journal-find-file                  'find-file)

(defun gunnar/today-note-file-name () (concat org-journal-dir (format-time-string org-journal-file-format)))
#+end_src

**** Agenda Date and Diary Handling
#+begin_src emacs-lisp
(setq! org-agenda-prefix-format      '((agenda . " %i %-16:c%?-12t% s")
                                       (todo   . " %i %-15:c [%e] ")
                                       (tags   . "                  %i")
                                       (search . " %i %-20:c"))
       org-agenda-scheduled-leaders   '("" "") ;; original:   ("Scheduled: " "Sched.%2dx: ")
       org-agenda-include-diary       t
       org-agenda-start-on-weekday    1
       diary-date-forms              '((month     "/"  day   "[^/0-9]")
                                       (month     "/"  day   "/"   year "[^0-9]")
                                       (monthname " *" day   "[^,0-9]")
                                       (monthname " *" day   ", *" year "[^0-9]")
                                       (dayname   "\\W")
                                       (year      "/"  month "/"   day  "[^,0-9]")))
#+end_src

**** org-agenda-files
#+begin_src emacs-lisp
(setq! org-agenda-files                       (cons
                                               org-journal-dir
                                               (mapcar (lambda (x) (concat org-directory x))
                                                       '("inbox-akiko.org"
                                                         "inbox-galaxy.org"
                                                         "inbox-s7.org"
                                                         "inbox-s11.org"
                                                         "inbox-gistaone.org"
                                                         "habits.org"
                                                         "tickler.org"
                                                         "projects.org"))))
#+end_src

**** org-agenda-custom-commands
#+begin_src emacs-lisp
(setq! org-agenda-custom-commands
       '(("P" "Private Agenda " ((agenda    ""
                                            ((org-agenda-skip-function            '(org-agenda-skip-entry-if 'deadline))
                                             (org-deadline-warning-days           2)))
                                 (todo      "NEXT"
                                            ((org-agenda-overriding-header        "Next Actions")
                                             (org-agenda-sorting-strategy         '(priority-down))
                                             (org-agenda-skip-function            '(org-agenda-skip-entry-if 'deadline))
                                             (org-agenda-todo-keyword-format      "")))
                                 (tags-todo "CATEGORY=\"inbox\""
                                            ((org-agenda-sorting-strategy         '(priority-down))
                                             (org-agenda-overriding-header        "Inbox")
                                             (org-agenda-todo-keyword-format      "")))
                                 (todo      "WAIT"
                                            ((org-agenda-sorting-strategy         '(priority-down))
                                             (org-agenda-overriding-header        "Waiting For")
                                             (org-agenda-todo-keyword-format      "")))
                                 (tags      "CLOSED>=\"<today>\""
                                            ((org-agenda-overriding-header "Completed today")
                                             (org-agenda-todo-keyword-format      ""))))
          ((org-agenda-start-day "+0d")(org-agenda-span 'day)))

         ("W" "Work Agenda"     ((agenda)
                                 (tags-todo "@ista+TODO=\"NEXT\""
                                            ((org-agenda-sorting-strategy         '(priority-down))
                                             (org-agenda-overriding-header        "Next Actions")
                                             (org-agenda-todo-keyword-format      "")))
                                 (tags-todo "@ista+TODO=\"WAIT\""
                                            ((org-agenda-sorting-strategy         '(priority-down))
                                             (org-agenda-overriding-header        "Waiting For")
                                             (org-agenda-todo-keyword-format      "")))
                                 (tags-todo "@ista+TODO=\"TODO\""
                                            ((org-agenda-overriding-header        "TODO Items")
                                             (org-agenda-todo-keyword-format      ""))))
          ((org-agenda-span 'day)))

         ("c" "Inbox" tags-todo "CATEGORY=\"inbox\""
          ((org-agenda-overriding-header "Inbox")))

         ("g" "Get Things Done (GTD)" ((tags      "CATEGORY=\"inbox\""
                                                  ((org-agenda-prefix-format            "  %?-12t% s")
                                                   (org-agenda-hide-tags-regexp         "inbox")
                                                   (org-agenda-overriding-header        "\nInbox: clarify and organize\n")))))))
#+end_src

*** Functions
**** Helpers
#+begin_src emacs-lisp
(defun gunnar/org-copy-todo-to-today ()
  (interactive)
  (let ((org-refile-keep t) ;; Set this to nil to delete the original!
        (org-capture-templates '(("a" "Journal Task"   entry (file+headline gunnar/today-note-file-name "Tasks")
                                  "* [%<%H:%M>] %(gunnar/org-entry-info) %?"
                                  :immediate-finish t)))
        (org-refile-targets '(((gunnar/today-note-file-name) :maxlevel . 1)))
        pos)
    (with-current-buffer (find-file-noselect (gunnar/today-note-file-name))
      (goto-char (point-min))
      (unless (re-search-forward "^\\* Tasks\\b" nil t)
        (goto-char (point-max))
        (unless (bolp) (newline)) ; Make sure we're on a new line
        (insert "* Tasks\n"))
      (save-buffer))
    (setq pos (point))
    ;; Only refile if the target file is different than the current file
    (unless (equal (file-truename (gunnar/today-note-file-name))
                   (file-truename (buffer-file-name)))
      (org-refile nil nil (list "Tasks" (gunnar/today-note-file-name) nil pos)))))

;; (add-to-list 'org-after-todo-state-change-hook
;;              (lambda ()
;;                (when (equal org-state "DONE")
;;                  (gunnar/org-copy-todo-to-today))))

(defun my-org-agenda-skip-all-siblings-but-first ()
  "Skip all but the first non-done entry."
  (let (should-skip-entry)
    (unless (org-current-is-todo) (setq should-skip-entry t))
    (save-excursion
      (while (and (not should-skip-entry) (org-goto-sibling t))
        (when (org-current-is-todo)
          (setq should-skip-entry t))))
    (when should-skip-entry (or (outline-next-heading) (goto-char (point-max))))))

(defun org-current-is-todo () (string= "TODO" (org-get-todo-state)))

(defun gunnar/get-clocked-time ()
  (interactive)
  (let ((hours (/ (org-clock-get-clocked-time) 60))
        (minutes (% (org-clock-get-clocked-time) 60)))
    (format "[%02d:%02d]" hours minutes)))

(advice-add 'org-refile :after (lambda (&rest _) (org-save-all-org-buffers)))

(defun gunnar/org-clock-expired-p ()
  (when (org-clocking-p)
    (let ((effort-in-minutes (org-duration-to-minutes org-clock-effort))
	  (clocked-time (org-clock-get-clocked-time)))
      (if (or (null effort-in-minutes) (zerop effort-in-minutes))
          nil
        (>= clocked-time effort-in-minutes)))))
#+end_src

**** align-tags
#+begin_src emacs-lisp
(defun gunnar/org-align-all-tags ()
  "Align all tags in the current buffer."
  (interactive)
  (org-agenda-align-tags))
#+end_src
**** agenda-views
#+begin_src emacs-lisp
(defun gunnar/org-agenda-show-inbox         (&optional arg) (interactive "P") (org-agenda arg "c"))
(defun gunnar/org-agenda-show-today         (&optional arg) (interactive "P") (org-agenda arg "a"))
(defun gunnar/org-agenda-show-today-ista    (&optional arg) (interactive "P") (org-agenda arg "b"))
#+end_src

*** Keybindings
#+begin_src emacs-lisp
(map! :leader :desc "Show agenda for today"         :n "Gt"   #'gunnar/org-agenda-show-today)
(map! :leader :desc "Show ista agenda for today"    :n "Gm"   #'gunnar/org-agenda-show-today-ista)
(map! :leader :desc "Show Inbox"                    :n "Gi"   #'gunnar/org-agenda-show-inbox)
(map! :leader :desc "Align all tags in buffer"      :n "mwa"  #'gunnar/org-align-all-tags)
#+end_src

** Capture & Refile
*** Settings
#+begin_src emacs-lisp
(setq! gunnar/inbox-file-name (concat org-directory
                                      (if (string-prefix-p "akiko" (system-name) t)
                                          "inbox-akiko.org"
                                        "inbox-gistaone.org")))
(after! org
  (setq org-default-notes-file    gunnar/inbox-file-name)

  (setq org-capture-templates     '(("t" "Task"           entry (file org-default-notes-file)
                                     "* TODO %^{Description} %? %a\n %i\n :PROPERTIES: :CREATED: %U :END:\n"
                                     :immediate-finish t)
                                    ("n" "Note"           entry (file org-default-notes-file)
                                     "* %^{Description} %? %i\n :PROPERTIES: :CREATED: %U :END:\n"
                                     :immediate-finish t)
                                    ("j" "Journal Entry"  entry (file gunnar/today-note-file-name)
                                     "* %^{Description} %? %i\n :PROPERTIES: :CREATED: %U :END:\n"
                                     :immediate-finish t)
                                    ("l" "Bookmarks"      entry (file "")
                                     "** %(org-cliplink-capture)%?\n :PROPERTIES: :CREATED: %U :END:\n"
                                     :immediate-finish t)
                                    ("s" "Step"           entry (clock)
                                     "* %^{Description}%?%i\n :PROPERTIES: :CREATED: %U :END:\n")
                                    ("m" "Metrics" table-line (file+headline (concat org-directory "exercises.org") "November")
                                     "| %U         | %^{Yoga} | %^{Crunches} | %^{Push-Ups} | %^{Pull-Ups} | %^{Boxing} | %^{Knee bends} | %^{Rolls} | %^{B} | Z |  |")
                                    ;; ("l" "Bookmarks"      entry (file+headline (lambda () (gunnar/daily-note)) "Bookmarks")
                                    ;;  "** %(org-cliplink-capture)%?\n" :unnarrowed t)
                                    ("x" "org-protocol"   entry (file+headline org-default-notes-file "Inbox")
                                     "* TODO Review %c\n%U\n%i\n :PROPERTIES: :CREATED: %U :END:\n"
                                     :immediate-finish))

        org-refile-targets        '(("~/org/projects.org"                     :maxlevel . 3)
                                    (gunnar/today-note-file-name              :maxlevel . 3)
                                    ("~/org/someday.org"                      :maxlevel . 3)
                                    ("~/org/bookmarks.org"                    :maxlevel . 5)
                                    ("~/org/tickler.org"                      :level    . 1))))
#+end_src

*** Functions
#+begin_src emacs-lisp
(defun gunnar/org-capture-task (&optional arg) (interactive "P") (org-capture arg "t"))
(defun gunnar/org-capture-note (&optional arg) (interactive "P") (org-capture arg "n"))
#+end_src

*** Keybindings
#+begin_src emacs-lisp
(map! :leader :desc "Capture Task"                  :n "Gc" #'gunnar/org-capture-task)
(map! :leader :desc "Capture Note"                  :n "Gn" #'gunnar/org-capture-note)
#+end_src

** Calendar
*** Settings
#+begin_src emacs-lisp
(setq! calendar-date-display-form   '((format "%s-%.2d-%.2d" year (string-to-number month) (string-to-number day)))
       calendar-date-style          'iso
       calendar-week-start-day      1
       calendar-mark-holidays-flag  1)

(setq! calendar-intermonth-text     '(propertize
                                      (format "%2d"
                                              (car
                                               (calendar-iso-from-absolute
                                                (calendar-absolute-from-gregorian (list month day year)))))
                                      'font-lock-face 'font-lock-constant-face)
       calendar-intermonth-header   (propertize "WK" 'font-lock-face 'font-lock-keyword-face))
(setq! calendar-longitude 13.331850
       calendar-latitude  52.446540
       calendar-location-name "Berlin, DE")
#+end_src

*** Functions
#+begin_src emacs-lisp
(defun gunnar/all-calendars-to-diary ()
  (interactive)
  (progn
    (gunnar/some-calendar-to-diary (password-store-get "private/cloud.bastkowski.name/calendar-gunnar-url"))))

(defun gunnar/some-calendar-to-diary (calendar-url)
  (let ((ical-filename (make-temp-file "calendar.ics")))
    (progn
      (url-copy-file calendar-url ical-filename t)
      (set-buffer (find-file-noselect ical-filename))
      (icalendar-import-buffer diary-file t)
      (kill-buffer)
      'ok)))
#+end_src

*** Keybindings
#+begin_src emacs-lisp
(map! :leader :desc "Show calendar"         :n "y" #'calendar)
(map! :leader :desc "All calendars to diary"        :n "Gc" #'gunnar/all-calendars-to-diary)
#+end_src

** Journal
*** Open Tasks
**** TODO org-enable-asciidoc-support t
**** TODO org-enable-bootstrap-support t
**** TODO org-enable-github-support t
**** TODO jiralib-url "https://jira.mobimeo.com"
**** TODO org-enable-sticky-header nil
**** TODO org-start-notification-daemon-on-startup t)

** Pomodoro
*** Settings
#+begin_src emacs-lisp
(setq! org-pomodoro-finished-sound "/Users/gunnar/Media/Sounds/mixkit-correct-answer-reward-952.wav"
       org-pomodoro-start-sound    "/Users/gunnar/Media/Sounds/mixkit-positive-notification-951.wav")
#+end_src
** Presentations
#+begin_src emacs-lisp
(after! org
  ;; Keep Org buffers monospace by default; presentation tweaks handle
  ;; variable-pitch themselves when needed.
  (remove-hook 'org-mode-hook #'variable-pitch-mode))

(use-package! visual-fill-column
  :defer t
  :init
  (setq visual-fill-column-center-text t
        visual-fill-column-width 110))

(defun gunnar/org-present-prepare-slide (&rest _)
  (org-overview)
  (org-show-entry)
  (org-show-children))

(defvar-local gunnar/org-present--saved-face-remapping nil
  "Face remapping to restore after leaving `org-present-mode'.")

(defun gunnar/org-present-start ()
  (setq gunnar/org-present--saved-face-remapping face-remapping-alist)
  (setq-local face-remapping-alist
              '((default (:height 1.5) variable-pitch)
                (header-line (:height 4.0) variable-pitch)
                (org-document-title (:height 1.75) org-document-title)
                (org-code (:height 1.55) org-code)
                (org-verbatim (:height 1.55) org-verbatim)
                (org-block (:height 1.25) org-block)
                (org-block-begin-line (:height 0.7) org-block)))
  (setq header-line-format " ")
  (org-display-inline-images)
  (visual-fill-column-mode 1))

(defun gunnar/org-present-end ()
  (setq-local face-remapping-alist gunnar/org-present--saved-face-remapping)
  (setq header-line-format nil)
  (org-remove-inline-images)
  (visual-fill-column-mode 0))

(add-hook 'org-present-mode-hook #'gunnar/org-present-start)
(add-hook 'org-present-mode-quit-hook #'gunnar/org-present-end)
(add-hook 'org-present-after-navigate-functions #'gunnar/org-present-prepare-slide)
#+end_src

** Babel
*** Settings
#+begin_src emacs-lisp
(setq org-babel-load-languages  '((emacs-lisp . t)
                                  (awk . t)
                                  (ditaa . t)
                                  (dot . t)
                                  (java . t)
                                  (dot . t)
                                  (plantuml . t)
                                  (ruby . t)))
#+end_src

** Brain
*** Settings
#+begin_src emacs-lisp
(setq! org-brain-include-file-entries t)
#+end_src

** Reveal
*** Settings
#+begin_src emacs-lisp
(setq! org-re-reveal-title-slide          "auto"
       org-re-reveal-transition           "convex"
       org-re-reveal-hlevel               2
       org-re-reveal-width                1920
       org-re-reveal-center               nil)
#+end_src

** Roam
*** Functions
#+begin_src emacs-lisp
(defun org-roam-node-insert-immediate (arg &rest args)
  (interactive "P")
  (let ((args (cons arg args))
        (org-roam-capture-templates (list (append (car org-roam-capture-templates)
                                                  '(:immediate-finish t)))))
    (apply #'org-roam-node-insert args)))
#+end_src

*** Keybindings
#+begin_src emacs-lisp
(map! :leader :desc "Insert node in background"       :n "nrI"  #'org-roam-node-insert-immediate)
#+end_src


* Postgres
** Packages
#+begin_src emacs-lisp
(use-package! pg)
(use-package! pgmacs)
#+end_src


* Project
** Keybindings
#+BEGIN_SRC emacs-lisp
#+END_SRC

** Settings
#+begin_src emacs-lisp
(setq! projectile-project-search-path '("~/git/rdm/" "~/git/gbastkowski/"))
(setq! lsp-file-watch-threshold nil)
#+end_src

** Functions
#+BEGIN_SRC emacs-lisp
(defun split-window-right-and-focus ()
  "Split the window horizontally and focus the new window."
  (interactive)
  (split-window-right)
  (windmove-right)
  (when (and (boundp 'golden-ratio-mode)
             (symbol-value golden-ratio-mode))
    (golden-ratio)))

(defun gunnar-open-terminal-right ()
  (interactive)
  (progn
    (split-window-right-and-focus)
    (projectile-run-vterm)))

(defun gunnar/toggle-window-split ()
  "Toggle between horizontal and vertical split for two windows."
  (interactive)
  (if (= (count-windows) 2)
      (let* ((this-win-buffer (window-buffer))
             (next-win-buffer (window-buffer (next-window)))
             (this-win-edges (window-edges (selected-window)))
             (next-win-edges (window-edges (next-window)))
             (this-win-2nd (not (and (<= (car this-win-edges)
                                         (car next-win-edges))
                                     (<= (cadr this-win-edges)
                                         (cadr next-win-edges)))))
             (splitter
              (if (= (car this-win-edges)
                     (car (window-edges (next-window))))
                  'split-window-horizontally
                'split-window-vertically)))
        (delete-other-windows)
        (let ((first-win (selected-window)))
          (funcall splitter)
          (if this-win-2nd (other-window 1))
          (set-window-buffer (selected-window) this-win-buffer)
          (set-window-buffer (next-window) next-win-buffer)
          (select-window first-win)
          (if this-win-2nd (other-window 1))))
    (message "Can only toggle split with exactly 2 windows")))
#+END_SRC


* LaTeX
** Settings

#+begin_src emacs-lisp
(after! tex
  (setq TeX-command-list
        (append TeX-command-list
                '(("LatexMk" "latexmk %s" TeX-run-TeX nil t
                   :help "Run latexmk to compile the document")
                  ("LatexMk-PVC" "latexmk -pvc %s" TeX-run-TeX nil t
                   :help "Run latexmk to compile the document"))))
  (setq TeX-command-default "LatexMk-PVC"))
#+end_src


* Web
** Settings
#+begin_src emacs-lisp
(setq! web-mode-markup-indent-offset 2
       web-mode-css-indent-offset 2
       web-mode-sql-indent-offset 2
       web-mode-code-indent-offset 2)
#+end_src



* Dashboard
** Settings
*** doom dashboard
#+begin_src emacs-lisp
(setq! +doom-dashboard-menu-sections
  '(("Recently opened files"
     :icon (nerd-icons-faicon "nf-fa-file_text" :face 'doom-dashboard-menu-title)
     :action recentf-open-files)
    ("Reload last session"
     :icon (nerd-icons-octicon "nf-oct-history" :face 'doom-dashboard-menu-title)
     :when (cond ((modulep! :ui workspaces)
                  (file-exists-p (expand-file-name persp-auto-save-fname persp-save-dir)))
                 ((require 'desktop nil t)
                  (file-exists-p (desktop-full-file-name))))
     :action doom/quickload-session)
    ("Open inbox"
     :icon (nerd-icons-octicon "nf-oct-calendar" :face 'doom-dashboard-menu-title)
     :when (fboundp 'gunnar/org-agenda-show-inbox)
     :action gunnar/org-agenda-show-inbox)
    ("Open today's agenda"
     :icon (nerd-icons-octicon "nf-oct-calendar" :face 'doom-dashboard-menu-title)
     :when (fboundp 'gunnar/org-agenda-show-today)
     :action gunnar/org-agenda-show-today)
    ("Open org-agenda"
     :icon (nerd-icons-octicon "nf-oct-calendar" :face 'doom-dashboard-menu-title)
     :when (fboundp 'org-agenda)
     :action org-agenda)
    ("Open project"
     :icon (nerd-icons-octicon "nf-oct-briefcase" :face 'doom-dashboard-menu-title)
     :action projectile-switch-project)
    ("Jump to bookmark"
     :icon (nerd-icons-octicon "nf-oct-bookmark" :face 'doom-dashboard-menu-title)
     :action bookmark-jump)
    ("Open private configuration"
     :icon (nerd-icons-octicon "nf-oct-tools" :face 'doom-dashboard-menu-title)
     :when (file-directory-p doom-user-dir)
     :action doom/open-private-config)
    ("Open documentation"
     :icon (nerd-icons-octicon "nf-oct-book" :face 'doom-dashboard-menu-title)
     :action doom/help)))
#+end_src

*** emacs-dashboard
#+begin_src emacs-lisp
(setq! dashboard-banner-logo-title          "Welcome to Emacs!"
       dashboard-items                      '((agenda . 20)
                                              (recents . 10)
                                              (bookmarks . 5)
                                              (registers . 10))
       dashboard-icon-type                  'nerd-icons
       dashboard-display-icons-p            t
       dashboard-set-heading-icons          t
       dashboard-set-file-icons             t
       dashboard-week-agenda                nil
       dashboard-startup-banner             'official
       dashboard-vertically-center-content  t
       dashboard-startupify-list            '(dashboard-insert-banner
                                              dashboard-insert-newline
                                              ;; dashboard-insert-banner-title
                                              ;; dashboard-insert-newline
                                              dashboard-insert-navigator
                                              ;; dashboard-insert-newline
                                              ;; dashboard-insert-init-info
                                              dashboard-insert-items
                                              dashboard-insert-newline
                                              dashboard-insert-footer))

;; Format: "(icon title help action face prefix suffix)"
(setq! dashboard-navigator-buttons
      `(;; line 1
        ((,(nerd-icons-octicon "nf-oct-clock" :height 1.1 :v-adjust 0.0)
          "ista Time"
          "Manage ista Time sheets"
          (lambda (&rest _) (browse-url "https://time.ista.com:44300/fiori#TimeEntry-change")))
         ;; ("★" "Star" "Show stars" (lambda (&rest _) (show-stars)) warning)
         ;; ("?" "" "?/h" #'show-help nil "<" ">")
         )
        ;; line 2
        ((,(nerd-icons-octicon "nf-oct-inbox" :height 1.1 :v-adjust 0.0)
          "Inbox"
          ""
          (lambda (&rest _) (gunnar/org-agenda-show-inbox)))
         )))
#+end_src

** Keybindings
#+begin_src emacs-lisp
(map! :leader :desc "Open Dashboard"      :n "bh" #'dashboard-open
      :leader :desc "Open Doom Dashboard" :n "bH" #'+doom-dashboard/open)
#+end_src


* Documentation
Whenever you reconfigure a package, make sure to wrap your config in an
`after!' block, otherwise Doom's defaults may override your settings. E.g.

  (after! PACKAGE
    (setq x y))

The exceptions to this rule:
- Setting file/directory variables (like `org-directory')
- Setting variables which explicitly tell you to set them before their
  package is loaded (see 'C-h v VARIABLE' to look up their documentation).
- Setting doom variables (which start with 'doom-' or '+').

Here are some additional functions/macros that will help you configure Doom.
- `load!' for loading external *.el files relative to this one
- `use-package!' for configuring packages
- `after!' for running code after a package has loaded
- `add-load-path!' for adding directories to the `load-path', relative to
  this file. Emacs searches the `load-path' when you load packages with
  `require' or `use-package'.
- `map!' for binding new keys

To get information about any of these functions/macros, move the cursor over
the highlighted symbol at press 'K' (non-evil users must press 'C-c c k').
This will open documentation for it, including demos of how they are used.
Alternatively, use `C-h o' to look up a symbol (functions, variables, faces,
etc).

You can also try 'gd' (or 'C-c c d') to jump to their definition and see how
they are implemented.
